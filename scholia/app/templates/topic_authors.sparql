# tool: scholia
#defaultView:Table
SELECT ?count ?author ?authorLabel (COALESCE(?orcid_, CONCAT("orcid-search/quick-search/?searchQuery=", ?authorLabel)) AS ?orcid) 
WITH {
  SELECT DISTINCT ?author (count(?work) as ?count) WHERE {
    { ?work wdt:P921/wdt:P31*/wdt:P279* wd:{{ q }} . }
    union { ?work wdt:P921/wdt:P361+ wd:{{ q }} . }
    union { ?work wdt:P921/wdt:P1269+ wd:{{ q }} . }
    ?work wdt:P50 ?author .
  }
  GROUP BY ?author
  ORDER BY DESC(?count)
  LIMIT 200
} AS %result
WHERE {
  INCLUDE %result
          
  # Include optional ORCID iD
  OPTIONAL { ?author wdt:P496 ?orcid_ . }
  SERVICE wikibase:label { bd:serviceParam wikibase:language "en,da,de,es,fr,jp,nl,no,ru,sv,zh" . } 
}
ORDER BY DESC(?count)
