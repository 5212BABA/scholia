PREFIX target: <http://www.wikidata.org/entity/{{ q }}>

SELECT ?publication_date 
?work ?title (CONCAT("/work/", SUBSTR(STR(?work), 32)) AS ?workUrl)
WITH{
  SELECT
    (MIN(?publication_date_) AS ?publication_date) ?work
  WHERE {
    SERVICE bd:sample { ?work wdt:P4510 target: . bd:serviceParam bd:sample.limit 100000 } .
    ?work wdt:P577 ?publication_datetime .
    BIND(xsd:date(?publication_datetime) AS ?publication_date_)
  }
  GROUP BY ?work
  ORDER BY DESC(?publication_date)
  LIMIT 200
} AS %result
WHERE {
  INCLUDE %result
  ?work wdt:P1476 ?title. 
  FILTER (LANG(?title) = "en")
}
ORDER BY DESC(?publication_date)
LIMIT 200
